//  Create a landing table for telemetry
.create table landing(document:dynamic)

//  Create the mapping from JSON ingestion to landing table
.create table landing ingestion json mapping 'landingMapping' '[{"column":"document","path":"$","datatype":"dynamic"}]'

//  Alter retention policy as this is only for end-user queries
.alter table landing policy retention "{'SoftDeletePeriod': '0:10:00', 'Recoverability':'Enabled'}"

//  Alter ingestion policy to ingest more often than default (5 minutes)
.alter table landing policy ingestionbatching "{'MaximumBatchingTimeSpan': '0:0:10'}"

//  Look at the landing table
landing
| limit 12

//  Function used to parse the raw telemetry into an intermediate table
.create-or-alter function parseRawTelemetry(){
    landing
    //  Project strong type message meta data
    | project gatewayId=tostring(document.gatewayId),
        messageId=tostring(document.messageId),
        messageTimestamp=todatetime(document.messageTimestamp),
        hubSequenceNumber=toint(document['x-opt-sequence-number']),
        hubEnqueudTime=todatetime(document['x-opt-enqueued-time']),
        hubOffset=toint(document['x-opt-offset']),
        //  Keep events in JSON format / dynamic type
        events=document.events
    //  Explode each event to a different record
    | mv-expand events
    //  Project strong type event meta data
    | extend eventTimestamp=todatetime(events.eventTimestamp)
    //  Parse 2 components out of droneId
    | parse tostring(events.droneId) with droneVersion ";" droneId
    | extend device=tostring(events.device)
    //  Keep the specific of the device measurement in JSON / dynamic
    | extend measurement=events.measurement
    //  Remove the events dynamic
    | project-away events
}

//  Create an intermediate table for telemetry
.create table genericTelemetry(
    gatewayId:string,
    messageId:string,
    messageTimestamp:datetime,
    hubSequenceNumber:int,
    hubEnqueudTime:datetime,
    hubOffset:int,
    eventTimestamp:datetime,
    droneVersion:string,
    droneId:string,
    device:string,
    measurement:dynamic)

//  Alter retention policy as this is also only for end-user queries
.alter table genericTelemetry policy retention "{'SoftDeletePeriod': '0:10:00', 'Recoverability':'Enabled'}"

//  Create an update policy to transfer landing to genericTelemetry
.alter table genericTelemetry policy update
@'[{"IsEnabled": true, "Source": "landing", "Query": "parseRawTelemetry", "IsTransactional": true, "PropagateIngestionProperties": false}]'

.show table landing extents

.show table genericTelemetry extents 

genericTelemetry